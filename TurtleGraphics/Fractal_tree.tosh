// Variables: yPos, lineSize, ratio, branches, angle, minLineSize

// Sierpinski triangle: yPos -55, lineSize 110, ratio 0.5, branches 3, angle 120, minLineSize 1
// H tree: yPos 0, lineSize 120, ratio 0.7, branches 2, angle 180, minLineSize 4

when flag clicked
pen up
clear
set pen color to #333333
set yPos to -60
set lineSize to 75
set ratio to 0.7
set branches to 2
set angle to 45
set minLineSize to 2
set x to 0
set y to yPos
show
point in direction 180
pen down
move (lineSize / ratio) steps
pen up
tree fractal 0 yPos 0 lineSize
hide

define tree fractal (xArg) (yArg) (directionArg) (sizeArg)
if <sizeArg > minLineSize> then
	loop 0 xArg yArg directionArg sizeArg
end

define loop (i) (xArg) (yArg) (directionArg) (sizeArg)
if <i < branches> then
	set x to xArg
	set y to yArg
	point in direction ((directionArg - ((angle * (branches - 1)) / 2)) + (i * angle))
	pen down
	move sizeArg steps
	pen up
	tree fractal x position y position direction (sizeArg * ratio)
	loop (i + 1) xArg yArg directionArg sizeArg
end